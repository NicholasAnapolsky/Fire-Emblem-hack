EventPointerTable(0x07,ThisChapter) //0x7 is Prologue Events

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

//Variable Definitions
//Characters
#define Loydie 0x1
#define Fauna 0xf
#define Silvester 0x2
#define Dirk ONeill

//Conversations

//EnemyUnits
#define grunt1 0x80
#define grunt2 0x81
#define grunt3 0x82
#define grunt4 0x83
#define grunt5 0x84

//Music
#define SadMusic 0x002C
#define RecruitMusic 0x0030
#define SillyMusic 0x002A
#define EnemyAproach 0x0026
#define VillageDestroyed 0x00AB

//Event Ids
#define BossQuote 0x01

Units:
//Fauna using Ephiram as a NPC to start, she runs for escape, starts at position [3,5]
UNIT Fauna Thief Eirika Level(1, NPC, False) [3,3] 0x00 0x00 0x0 0x00 [IronSword, Vulnerary] [0x00, 0x00, 0xFF, 0x00]

//The boss Dirk using ONeill as a unit, starts at position [13,1]
UNIT Dirk Fighter 0x00 Level(10, Enemy, True) [14,3] 0x00 0x00 0x0 0x00 [SteelAxe] GuardTileAI 
UNIT

Units2:
//Loydie using Erika as a unit, starts at position [2,3]
UNIT Loydie EirikaLord Eirika Level(1, Ally, False) [3,4] 0x00 0x00 0x0 0x00 [Rapier, Vulnerary] NoAI 

//Silvester using Seth as a unit, starts at position [1,1]
UNIT Silvester Paladin Eirika Level(1, Ally, False) [4,4] 0x00 0x00 0x0 0x00 [SteelLance] NoAI 
UNIT

Grunts:
//Enemy units using Dirk as a captain, using regular AI
UNIT grunt1 Fighter Dirk Level(2, Enemy, True) [13,0] 0x00 0x00 0x0 0x00 [IronAxe] DefaultAI 
UNIT grunt2 Fighter Dirk Level(2, Enemy, True) [14,0] 0x00 0x00 0x0 0x00 [IronAxe] DefaultAI 
UNIT grunt3 Soldier Dirk Level(2, Enemy, True) [15,0] 0x00 0x00 0x0 0x00 [IronLance] [0x00, 0x03, 0x09, 0x00]

//Enemy unit using Dirk as a captain, using AI that makes it move when an ally gets close in
UNIT grunt4 Fighter Dirk Level(2, Enemy, True) [14,1] 0x00 0x00 0x0 0x00 [IronAxe] [0x00, 0x06, 0x00, 0x00]

//Enemy unit using Dirk as a captain, using AI that makes it move when an ally is in range
UNIT grunt5 Archer Dirk Level(1, Enemy, True) [15,2] 0x00 0x00 0x0 0x00 [IronBow] [0x00, 0x03, 0x09, 0x00]
UNIT


TurnBasedEvents:
OpeningTurnEvent(CasualModePrompt)
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x7,EirikaEphraimTalk,Eirika,Ephraim)
END_MAIN

LocationBasedEvents:
//CHECK_ACTIVEID Loydie
House(0x10, HouseNorthWest, 4, 1)
House(0x11, HouseNorthCentral, 5, 3)
House(0x12, HouseSouthCentral, 3, 6)
House(0x13, HouseNorthEast, 10, 2)
SHOP 0x0 PrlgVendor [2,1] 0x17
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
END_MAIN

Dunno:
//DO NOT TOUCH
WORD $00

Tutorial:
//DO NOT TOUCH
WORD $00

TrapData:
END_MAIN

ALIGN 4

BeginningScene:
MUSC SillyMusic
LOAD1 0x1 Units
Text(Forest, PreChapter_Loydie_and_Silv)
CAM1 Fauna //camera move to unit
CUMO Fauna
STAL 50
CURE
Text(Village, PreChapter_Fauna)
LOAD1 0x1 Units2
MOVE 0x01 Loydie [2,2]
MOVE 0x01 Silvester [1,1]
STAL 100

CAM1 Dirk
CUMO Dirk
STAL 50
CURE
MUSC EnemyAproach
Text(Plain, PreChapterDirk)
LOAD1 0x1 Grunts
MOVE 0x00 grunt1 [13,5]
MOVE 0x00 grunt2 [14,4]
MOVE 0x00 grunt3 [15,7]
MOVE 0x00 grunt4 [15,5]
MOVE 0x00 grunt5 [13,9]
STAL 50
MOVE 0x00 grunt5 [11,8]
STAL 50

Text(HouseBG, House1Destroy)
TILECHANGE 0
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt4 [10,6]
STAL 40
TILECHANGE 1
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt3 [9,7]
STAL 40
TILECHANGE 2
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt1 [5,7]
STAL 40
TILECHANGE 3
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt3 [8,8]
MOVE 0x00 grunt2 [11,5]
MOVE 0x00 grunt4 [13,6]
MOVE 0x00 grunt5 [13,2]
MOVE 0x00 Dirk [14,0]
Text(Village, AllyRealisation)
MOVE 0x00 Fauna [6,5]
STAL 50
REMA //go from text to map
EVBIT_T 0x7 //don't fade to black
ENDA

// Events
EirikaEphraimTalk:
MUSC SadMusic
TEXTSTART
	TEXTSHOW FaunaRecruit1
TEXTEND
MUSC RecruitMusic
TEXTSTART
	TEXTSHOW FaunaRecruit2
TEXTEND
CUSA 0xf //change unit side to ally
ENDA

EndingScene:
ENUT 0x71
ENUT 0x72
MUSC 0x33
SetBackground(Riverside)
TEXTSTART //not using macro because i don't want it to return to map
TEXTSHOW Prologue_Text_2
TEXTEND
FADI 10
//MNTS 0x0
//Write_Byte(3,$202bcf0+$1b)
MNC4 0x0
ENDA

VisitVillage:
ENDA

HouseNorthWest:
Text(HouseBG, House1)
NoFade
ENDA

HouseNorthCentral:
Text(HouseBG, House2)
NoFade
ENDA

HouseSouthCentral:
Text(HouseBG, House3)
NoFade
ENDA

HouseNorthEast:
Text(HouseBG, House4)
NoFade
ENDA

CasualModePrompt:
/*
TUTORIALTEXTBOXSTART
SVAL 0xB 0xFFFFFFFF
TEXTSHOW CasualAsk
TEXTEND
REMA
SVAL 0x7 0x1
BNE 0x1 0xC 0x7
ENUT 0xB0
LABEL 0x1
NoFade
*/
ENDA

// Manual Movement

// Scripted Fights

// Units

// Shop Data
PrlgArmory:
SHLI IronSword IronLance IronAxe IronBow KillingEdge KillerLance KillerAxe KillerBow

PrlgVendor:
SHLI Vulnerary

MESSAGE Events end at offset currentOffset