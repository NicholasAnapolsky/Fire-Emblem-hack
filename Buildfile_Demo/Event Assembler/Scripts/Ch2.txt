#include "ROM Buildfile.txt"

EventPointerTable(0x0D,Chapter2)

Chapter2:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN Units Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

Units:
UNIT Fauna Thief Eirika Level(1, NPC, False) [3,3] 0x00 0x00 0x0 0x00 [IronSword, Vulnerary] [0x00, 0x00, 0xFF, 0x00]
UNIT

Boss:
UNIT Dirk Fighter 0x00 Level(5, Enemy, True) [14,3] 0x00 0x00 0x0 0x00 [SteelAxe] GuardTileAI 
UNIT

Units2:
UNIT Lloyd EirikaLord Eirika Level(1, Ally, False) [3,4] 0x00 0x00 0x0 0x00 [Rapier, Vulnerary] NoAI 

UNIT Silvester Paladin Eirika Level(1, Ally, False) [4,4] 0x00 0x00 0x0 0x00 [IronSword, SteelLance] NoAI 
UNIT

Grunts:
//Enemy units using Dirk as a captain, using regular AI
UNIT grunt1 Fighter Dirk Level(1, Enemy, True) [13,0] 0x00 0x00 0x0 0x00 [IronAxe] DefaultAI 
UNIT grunt2 Fighter Dirk Level(1, Enemy, True) [14,0] 0x00 0x00 0x0 0x00 [IronAxe] DefaultAI 
UNIT grunt3 Soldier Dirk Level(1, Enemy, True) [15,0] 0x00 0x00 0x0 0x00 [IronLance] [0x00, 0x03, 0x09, 0x00]

//Enemy unit using Dirk as a captain, using AI that makes it move when an ally gets close in
UNIT grunt4 Fighter Dirk Level(1, Enemy, True) [14,1] 0x00 0x00 0x0 0x00 [IronAxe] [0x00, 0x06, 0x00, 0x00]

//Enemy unit using Dirk as a captain, using AI that makes it move when an ally is in range
UNIT grunt5 Archer Dirk Level(1, Enemy, True) [15,2] 0x00 0x00 0x0 0x00 [IronBow] [0x00, 0x03, 0x09, 0x00]
UNIT

MarxUnits:
UNIT Marx Necromancer 0x0 Level(20, Enemy, False) [14,8] 0x00 0x00 0x0 0x00 [IronSword] GuardTileAI 
UNIT

TurnBasedEvents:
END_MAIN

CharacterBasedEvents:
CharacterEventBothWays(0x7,LloydFaunaTalk,Lloyd,Fauna)
END_MAIN

LocationBasedEvents:
House(0x10, HouseNorthWest, 4, 1)
House(0x11, HouseNorthCentral, 5, 3)
House(0x12, HouseSouthCentral, 3, 6)
House(0x13, HouseNorthEast, 10, 2)
SHOP 0x0 PrlgVendor [2,1] 0x17
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
DefeatAll(EndingScene)
END_MAIN

Dunno:
//DO NOT TOUCH
WORD $00

Tutorial:
//DO NOT TOUCH
WORD $00

TrapData:
END_MAIN

ALIGN 4

BeginningScene:
MUSC SillyMusic
LOAD1 0x1 Units
LOAD1 0x1 Boss
Text(Forest, PreChapter_Loydie_and_Silv)
CAM1 Fauna //camera move to unit
CUMO Fauna
STAL 50
CURE
Text(Village, PreChapter_Fauna)
LOAD1 0x1 Units2
_SETVAL 3 1000
GIVEITEMTOMAIN 0x1
MOVE 0x01 Lloyd [2,2]
MOVE 0x01 Silvester [1,1]
STAL 100

CAM1 Dirk
CUMO Dirk
STAL 50
CURE
MUSC EnemyAproach
Text(Plain, PreChapterDirk)
LOAD1 0x1 Grunts
MOVE 0x00 grunt1 [13,5]
MOVE 0x00 grunt2 [14,4]
MOVE 0x00 grunt3 [15,7]
MOVE 0x00 grunt4 [15,5]
MOVE 0x00 grunt5 [13,9]
ENUN
MOVE 0x00 grunt5 [11,8]
ENUN

SetBackground(HouseBG)
TEXTSTART
	TEXTSHOW House1Destroy
TEXTEND
SOUN Stab
TEXTCONT
TEXTEND
ClearBackground
REMA

TILECHANGE 0
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt4 [10,6]
ENUN
TILECHANGE 1
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt3 [9,7]
ENUN
TILECHANGE 2
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt1 [5,7]
ENUN
TILECHANGE 3
SOUN VillageDestroyed
STAL 50

MOVE 0x00 grunt3 [8,8]
MOVE 0x00 grunt2 [11,5]
MOVE 0x00 grunt4 [13,6]
MOVE 0x00 grunt5 [13,2]
MOVE 0x00 Dirk [14,0] //[4,4] location for testing
ENUN
Text(Village, AllyRealisation)
MOVE 0x00 Fauna [6,5]
STAL 50
REMA //go from text to map
EVBIT_T 0x7 //don't fade to black
ENDA

EndingScene:
ENUT 0x71
ENUT 0x72
MUSC PostChapter
CLEA 0x0
FADI 10

CHECK_ALIVE Fauna
SETVAL 0x3 True
IFEQUAL 0x0 0xC 0x3
SetBackground(Plain)
TEXTSTART
	TEXTSHOW PostChapter1
TEXTEND	
ELSE 0x1
ENIF 0x0
SetBackground(Plain)
TEXTSTART
	TEXTSHOW PostChapter2
TEXTEND
ENIF 0x1

FADI 10
//MNTS 0x0
//Write_Byte(3,$202bcf0+$1b)
MNC2 0x01
ENDA

// Events
LloydFaunaTalk:
TEXTSTART
MUSC SadMusic
TEXTSHOW FaunaRecruit
TEXTEND
MUSC RecruitMusic
TEXTCONT
TEXTEND
CUSA Fauna //change unit side to ally
ENDA

VisitVillage:
ENDA

HouseNorthWest:
MUSC SadMusic
Text(HouseBG, House1)
NoFade
ENDA

HouseNorthCentral:
MUSC SadMusic
Text(HouseBG, House2)
NoFade
ENDA

HouseSouthCentral:
MUSC SadMusic
Text(HouseBG, House3)
NoFade
ENDA

HouseNorthEast:
MUSC SadMusic
Text(HouseBG, House4)
NoFade
ENDA

// Manual Movement

// Scripted Fights

// Units

// Shop Data
PrlgArmory:
SHLI IronSword IronLance IronAxe IronBow KillingEdge KillerLance KillerAxe KillerBow

PrlgVendor:
SHLI Vulnerary

MESSAGE Events end at offset currentOffset